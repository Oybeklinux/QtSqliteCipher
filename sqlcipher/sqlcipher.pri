# see http://sourceforge.net/projects/wxcode
CONFIG(release, debug|release):DEFINES *= NDEBUG

DEFINES += SQLITE_OMIT_LOAD_EXTENSION SQLITE_OMIT_COMPLETE SQLITE_ENABLE_FTS3 SQLITE_ENABLE_FTS3_PARENTHESIS \
    SQLITE_ENABLE_FTS4 SQLITE_ENABLE_FTS4_PARENTHESIS \
    SQLITE_ENABLE_FTS5 SQLITE_ENABLE_FTS5_PARENTHESIS \
    SQLITE_ENABLE_RTREE SQLITE_HAS_CODEC

!contains(CONFIG, largefile):DEFINES += SQLITE_DISABLE_LFS
contains(QT_CONFIG, posix_fallocate):DEFINES += HAVE_POSIX_FALLOCATE=1
winrt: DEFINES += SQLITE_OS_WINRT
winphone: DEFINES += SQLITE_WIN32_FILEMAPPING_API=1
qnx: DEFINES += _QNX_SOURCE

INCLUDEPATH +=  $$PWD
DEPENDPATH  += $$PWD

HEADERS += \
    $$PWD/sqlite3.h \
    $$PWD/sqlite3ext.h

SOURCES += \
    $$PWD/sqlite3.c

LIBS += -L$$PWD/lib -lcrypto-1_1-x64

TR_EXCLUDE += $$PWD/*
